#!Freemarker
# Deze query geeft een result set met eerste verblijfadres details die voldoen 
# aan de 'inzien' criteria van de Reva applicatie. Deze resultset wordt onder
# andere gebruikt om het Reva zoek scherm te vullen.
#
# Required Parameters:
# 
# Optional parameters:
#   gemeenteCode       de BAG gemeente code.
#   dossierNummer      het RNI dossiernummer.
#   bsn                het GBA BSN nummer.
#   vanafDatum         de datum vanaf
#   totenmetDatum      de datum tot en met.
#   postCode           de postcode.
#   huisNummer         het huidnummer
#   limit              het maximum aantal te tonen resultaten
#   offset             de offset in de totale result set van waar te starten 
#                      met het tonen van resultaten.
#
# REMARK:
#   The parameters 'stamColumnName' and 'stamIndexName' can be ignored safely  
#   when executing this query. They are used by the AStore framework:
#     'stamColumnName'  Will be replaced with the column name used by the
#                       framework. This column name tells the framework to
#                       replace the resultset with a set of annotation trees.
#     'stamIndexName'   Will be replaced with the actual index name used
#                       by the framework. This name is configuration hence
#                       the need to use a parameter.
#

PREFIX oa:   <http://www.w3.org/ns/oa#>  
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX reva: <http://data.reva.nl/2013/07/reva#>
PREFIX rela: <http://www.digimelding.nl/2013/11/relaties#>
PREFIX gem:  <http://www.bprbzk.nl/BRP/informatiebank/tabel33#>

SELECT ?${stamColumnName} ?gemeenteVanInschrijving <#if (toonBSN??)>?bsn</#if> ?dossierNummer ?registratieTijdstempel ?correctieTijdstempel ?woonPlaats ?openbareRuimte ?postCode ?huisNummer ?huisLetter ?huisNummerToevoeging ?aanduidingHuisNummer ?adresType ?idCodeObject ?idCodeNummerAanduiding ?bagGebruiksdoelen ?toelichting 
WHERE {
<#if dossierNummer??>
  GRAPH ?stamUri {
    ?dossierNummerAnnotatie rdf:type reva:DossierNummerAnnotatie ;
      oa:hasTarget ?stamUri ;
      oa:hasBody '${dossierNummer}' .

    BIND( '${dossierNummer}' AS ?dossierNummer )
  }
</#if>

<#if bsn??>
  GRAPH ?stamUri {
    ?bsnAnnotatie rdf:type reva:BSNAnnotatie ;
      oa:hasBody '${bsn}' ;
      oa:hasTarget ?stamUri .

    BIND( '${bsn}' AS ?bsn )
  }
</#if>

  GRAPH ?stamUri {
    ?stamUri rdf:type reva:RootAnnotatie ;
      oa:annotatedAt ?registratieTijdstempel .

    ?adresAnnotatie rdf:type reva:AdresAnnotatie ;
      reva:isLaatsteAdres 'marker' ;
      oa:hasTarget ?stamUri ;
      oa:annotatedAt ?correctieTijdstempel ;
      reva:aanmaakDatumInt ?aanmaakDatumAdres ;
      oa:hasBody ?body .

    <#if ((woonPlaats??) || (openbareRuimte??) || (postCode??) || (huisNummer??))>
      ?body rdf:type reva:AdresBody .
      <#if (woonPlaats??)>
        ?body reva:woonplaats '${woonPlaats}' .
      </#if>
      <#if (openbareRuimte??)>
        ?body reva:openbareruimte '${openbareRuimte}' .
      </#if>
      <#if (postCode??)>
        ?body reva:postcode '${postCode}' .
      </#if>
      <#if (huisNummer??)>
        ?body reva:huisnummer '${huisNummer}' .
      </#if>
    </#if>

    <#if (vanafDatum??) || (totenmetDatum??)>
      FILTER (
        <#if (vanafDatum??)>
          ( ?aanmaakDatumAdres >= ${vanafDatum?replace("-","")} )
        </#if>
        <#if (vanafDatum??) && (totenmetDatum??)>
          &&
        </#if>
        <#if (totenmetDatum??)>
          ( ?aanmaakDatumAdres <= ${totenmetDatum?replace("-","")} )
        </#if>
      ) .
    </#if>

<#if dossierNummer??>
<#else>
    OPTIONAL {
      ?dossierNummerAnnotatie rdf:type reva:DossierNummerAnnotatie ;
        oa:hasTarget ?stamUri ;
        oa:hasBody ?dossierNummer .
    }
</#if>

<#if bsn??>
<#else>
    OPTIONAL {
      ?bsnAnnotatie rdf:type reva:BSNAnnotatie ;
        oa:hasTarget ?stamUri ;
        oa:hasBody ?bsn .
    }
</#if>

    ?body reva:woonplaats ?woonPlaats ;
      reva:openbareruimte ?openbareRuimte ;
      reva:huisnummer ?huisNummer ;
      reva:bagIdCodePlaats ?idCodeObject .

    OPTIONAL {
      ?body reva:postcode ?postCode .
    }
    OPTIONAL {
      ?body reva:huisletter ?huisLetter .
    }
    OPTIONAL {
      ?body reva:huisnummerToevoeging ?huisNummerToevoeging .
    }
    OPTIONAL {
      ?body reva:aanduidingBijHuisnummer ?aanduidingHuisNummer .
    }
    OPTIONAL {
      ?body reva:adresType ?adresType .
    }
    OPTIONAL {
      ?body reva:toelichting ?toelichting .
    }
    OPTIONAL {
      ?body reva:bagIdCodeNummerAanduiding ?idCodeNummerAanduiding .
    }
    OPTIONAL {
      ?body reva:bagGebruiksdoelen ?bagGebruiksdoelen .
    }
    BIND( SUBSTR( str(?stamUri), 10) AS ?${stamColumnName} )
  }

  OPTIONAL {
    GRAPH ?stamUri {
      ?stamUri rdf:type reva:RootAnnotatie ;
        oa:annotatedBy ?agent .

      ?agent foaf:organisation ?gemeenteVanInschrijvingCode .
    }
	
    GRAPH <http://www.bprbzk.nl/BRP/informatiebank/tabel33> {
      ?gemeenteObject rdf:type gem:Gemeente ;
        gem:code ?gemeenteVanInschrijvingCode ;
        gem:naam ?gemeenteVanInschrijving .
    }
  }

<#if gemeenteCode??>
  GRAPH <http://www.digimelding.nl/2013/11/relaties> {
    <http://www.kadaster.nl/gemeente/${gemeenteCode}> rdf:type rela:Gemeente ;
      rela:heeftPlaats ?idCodeObject .
  }
</#if>
}
