#!Freemarker 
#
# Deze query migreet alle objecten per datatype voor het veld reva:aanmaakDatumInt
# terug. Eigenlijk moet deze alleen datatype van xsd:integer teruggeven maak als gevolg
# fouten in de templates kunnen er ook velden van het datatype xsd:string zijn. Deze
# laatste group is onderdeel van de uitgevoerde imigratie.
#
# Required Parameters:
#   (none)
# 
# Optional parameters:
#   limit              het maximum aantal te tonen resultaten
#
# REMARK:
#   The parameters 'stamIndexName' can be ignored safely when executing this 
#   query. They are used by the AStore framework:
#     'stamIndexName'   Will be replaced with the actual index name used
#                       by the framework. This name is configuration hence
#                       the need to use a parameter.
#

PREFIX dm:   <http://www.digimelding.nl/2013/08/dm#>
PREFIX oa:   <http://www.w3.org/ns/oa#>  
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>  
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>  
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX reva: <http://data.reva.nl/2013/07/reva#>
PREFIX rela: <http://www.digimelding.nl/2013/11/relaties#>
PREFIX gem:  <http://www.bprbzk.nl/BRP/informatiebank/tabel33#>

DELETE {
  GRAPH ?graphName {
    ?adresObj reva:aanmaakDatumInt ?existingDateValue .
  }
} 
INSERT {
  GRAPH ?graphName {
    ?adresObj reva:aanmaakDatumInt ?newDateValue .
  }
} 
WHERE {
  SELECT ?graphName ?adresObj ?existingDateValue ?newDateValue {
    GRAPH ?graphName {
      ?adresObj oa:annotatedAt ?timestamp ;
        reva:aanmaakDatumInt ?existingDateValue .
        
      FILTER ( datatype(?timestamp) = xsd:dateTime )
      FILTER ( datatype(?existingDateValue) != xsd:integer )
    
      BIND( YEAR(?timestamp)*10000 + MONTH(?timestamp)*100 + DAY(?timestamp) as ?newDateValue )
    }
  }
  LIMIT ${limit}
} 

  